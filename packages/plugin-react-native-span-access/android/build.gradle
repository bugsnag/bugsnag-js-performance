def isNewArchitectureEnabled() {
  return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

repositories {
  mavenLocal()
  mavenCentral()
  google()
}

apply plugin: 'com.android.library'
if (isNewArchitectureEnabled()) {
  apply plugin: 'com.facebook.react'
}

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 29)

  if (android.hasProperty('namespace')) {
    namespace 'com.bugsnag.reactnative.performance.nativespans'
  }

  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += ['src/newarch/java']
      } else {
        java.srcDirs += ['src/oldarch/java']
      }
    }
  }
}

dependencies {
  implementation 'com.facebook.react:react-native'
  implementation project(':bugsnag_react-native-performance')
  api 'com.bugsnag:bugsnag-android-performance:1.16.0'
}
